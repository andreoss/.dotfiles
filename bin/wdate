#!/usr/bin/env perl
use strict;
use warnings;
use POSIX        qw(setlocale strftime);
use List::Util   qw(sum);
use experimental qw(signatures postderef);

our @TZs = qw(
  America/Argentina/Buenos_Aires America/Asuncion America/Los_Angeles
  America/Caracas America/Mexico_City America/New_York America/Sao_Paulo
  Asia/Dubai Asia/Tokyo Europe/Berlin Europe/Sofia Europe/Lisbon Europe/Madrid
  Europe/London Europe/Moscow Europe/Tallinn Europe/Warsaw UTC
  Asia/Kolkata
);

sub print_table ($data) {

    my @widths = ();

    for my $row ( $data->@* ) {
        for my $i ( 0 .. $row->$#* ) {
            $widths[$i] = length( $row->[$i] )
              if length( $row->[$i] ) > ( $widths[$i] || 0 );
        }
    }

    for my $row (@$data) {
        for my $i ( 0 .. $row->$#* ) {
            printf "%-*s", $widths[$i] + 2, $row->[$i];
        }
        print "\n";
    }
}

my @data = ();
for my $tz (@TZs) {
    $ENV{'TZ'} = $tz;
    my ( $time, $offset, $day_name, $day, $month ) = split /\|/,
      strftime( "%H:%M|%z|%A|%d|%B", localtime() );

    push @data, [ $tz, $time, $offset, $day_name, $day, $month ];
}

@data = sort { $a->[2] <=> $b->[2] } @data;

print_table( \@data );
